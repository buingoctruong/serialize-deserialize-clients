plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
    id 'com.github.ben-manes.versions' version '0.28.0'
}

group = 'github.io.truongbn'
version = '0.0.1-SNAPSHOT'
mainClassName = 'github.io.truongbn.jsonclients.Cli'

java {
    sourceCompatibility = '17'
}

repositories {
    mavenCentral()
}

ext {
    jacksonVersion = '2.14.2'
    dslJsonVersion = '1.10.0'
    fastJsonVersion = '2.0.23'
    avajeJsonVersion = '1.3-RC1'
    jmhVersion = '1.36'
}

dependencies {
    // CLI and misc
    implementation group: 'io.airlift', name: 'airline', version: '0.9'
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.12.0'

    // Jackson
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: "${jacksonVersion}"
    implementation group: 'com.fasterxml.jackson.module', name: 'jackson-module-afterburner', version: "${jacksonVersion}"
    implementation group: 'com.fasterxml.jackson.module', name: 'jackson-module-blackbird', version: "${jacksonVersion}"
    implementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: "${jacksonVersion}"

    // FastJson
    implementation group: 'com.alibaba', name: 'fastjson', version: "${fastJsonVersion}"

    // DSL-json
    implementation group: 'com.dslplatform', name: 'dsl-json-java8', version: "${dslJsonVersion}"
    annotationProcessor group: 'com.dslplatform', name: 'dsl-json-java8', version: "${dslJsonVersion}"

    // avaje-jsonb
    implementation group: 'io.avaje', name: 'avaje-jsonb', version: "${avajeJsonVersion}"
    implementation group: 'io.avaje', name: 'avaje-jsonb-jackson', version: "${avajeJsonVersion}"
    annotationProcessor group: 'io.avaje', name: 'avaje-jsonb-generator', version: "${avajeJsonVersion}"

    // JMH
    implementation group: 'org.openjdk.jmh', name: 'jmh-core', version: "${jmhVersion}"
    annotationProcessor group: 'org.openjdk.jmh', name: 'jmh-generator-annprocess', version: "${jmhVersion}"
}

shadowJar {
    archiveFileName = 'app.jar'
    exclude 'META-INF/*.SF'
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
}

tasks.named('test') {
    doLast {
        def size = 0
        def formatStr = '%,10.2f'
        configurations.default.collect { it.length() / (1024 * 1024) }.each { size += it }

        def out = new StringBuffer()
        out << 'Total dependencies size:'.padRight(45)
        out << "${String.format(formatStr, size)} MiB\n\n"

        configurations
                .default
                .sort { -it.length() }
                .each {
                    out << "${it.name}".padRight(45)
                    out << "${String.format(formatStr, (it.length() / 1024))} KiB\n"
                }
        println(out)
    }
}